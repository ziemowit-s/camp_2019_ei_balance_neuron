load_file("nrngui.hoc")

objectvar g[20]         // max 20 graphs
ngraph = 0

proc addgraph() { local ii  // define subroutine to add a new graph
                // addgraph("variable", minvalue, maxvalue)
    ngraph = ngraph+1
    ii = ngraph-1
    g[ii] = new Graph()
    g[ii].size(0,tstop,$2,$3)
    g[ii].xaxis()
    g[ii].yaxis()
    g[ii].addvar($s1,1,0)
    g[ii].save_name("graphList[0].")
    graphList[0].append(g[ii])
}
//----------------------------------------------------------------------------
//  general parameters
//----------------------------------------------------------------------------

tstop = 100
dt=0.025
runStopAt = tstop
steps_per_ms = 1/dt
celsius = 36
v_init = -70

//Cuurent clamp pulse duration in presynaptic compartment
stdur=2
stamp = 0.1

//----------------------------------------------------------------------------
//  create compartments and insert passive properties
//----------------------------------------------------------------------------

create PRE,POST
//loop over all compartments to set identical parameters
forall {
  diam=10
  L=10
  insert pas

  Rm=5000
  g_pas=1/Rm
  e_pas=v_init
}

//----------------------------------------------------------------------------
//  insert presynaptic mechanisms
//----------------------------------------------------------------------------

access PRE      // insert Hodgk-Hux. Na+ and K+ currents for spikes
insert hh2
ek = -90
gnabar_hh2 = 0.1
gkbar_hh2 = 0.03

objectvar stim      // insert current injection
PRE stim = new IClamp(.5)

stim.del = 2
stim.dur = stdur
stim.amp = stamp

//----------------------------------------------------------------------------
//  insert postsynaptic mechansisms : Synapse
//----------------------------------------------------------------------------

objectvar c
c = new AMPA()          // create synapse
POST c.loc(0.5)         // assign postsynaptic compartment


// NetCon object to pass presynaptic events (spikes) to postsynaptic cell
objref ncl
ncl=new List()
//source, target, threshold, delay, weight
PRE ncl.append(new NetCon(&v(1), c, 0, 0, 0.0001))

c.gmax      = 0.0001 // (umho)       maximum conductance
c.tau		= 5
c.TRise		= 2  // Rise time
c.Erev		= 0  // Reversal
c.Deadtime	= 1



/// Insert HH in POST. Play with synaptic weights and PRE spiking frequency( how will you change that??).
/// When does POST spike?

//----------------------------------------------------------------------------
//  Set up graphs
//----------------------------------------------------------------------------

proc update_tauca() {
  stim.amp = stamp
}

xpanel("Parameters")
xvalue("Clamp amplitude","stamp",1,"update_stamp()",1,1)
xpanel()

nrnmainmenu()         // create main menu
nrncontrolmenu()      // create control menu

addgraph("PRE.v(0.5)",-90,40)
addgraph("c.g",0,0.001)
addgraph("c.i",0,-0.01)
addgraph("POST.v(0.5)",v_init,v_init+6)




